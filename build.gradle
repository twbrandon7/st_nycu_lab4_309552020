plugins {
    id 'java'
}

def getLatestHash = { ->
    try {
        def stdout = new ByteArrayOutputStream()
        exec {
            commandLine 'git', 'rev-parse', '--short', 'HEAD'
            standardOutput = stdout
        }
        return stdout.toString().trim()
    }
    catch (ignored) {
        return null;
    }
}

def getLatestTag = { ->
    try {
        def stdout = new ByteArrayOutputStream()
        exec {
            commandLine 'git', 'describe', '--tags'
            standardOutput = stdout
        }
        return stdout.toString().trim()
    }
    catch (ignored) {
        return null;
    }
}

def getVersionNumber = { ->
    if (!getLatestTag()) {
        return 'SNAPSHOT' + "-" + getLatestHash()
    } else {
        return getLatestTag()
    }
}

group 'tw.edu.nctu.cs.lxchen'
version "${getVersionNumber()}"

repositories {
    mavenCentral()
}

dependencies {
    testCompile "org.junit.jupiter:junit-jupiter-api:5.4.2"
    testCompile "org.junit.jupiter:junit-jupiter-engine:5.4.2"
    testCompile "org.junit.jupiter:junit-jupiter-params:5.4.2"
    testCompile "org.mockito:mockito-core:3.6.0"
    testCompile "org.mockito:mockito-junit-jupiter:3.6.0"
    testCompile "org.mockito:mockito-inline:3.6.0"
}

test {
    useJUnitPlatform()
    reports {
        html.enabled = true
    }
}

jar {
    manifest {
        attributes 'Main-Class': 'tw.edu.nctu.cs.lxchen.lab4.Main'
    }
}